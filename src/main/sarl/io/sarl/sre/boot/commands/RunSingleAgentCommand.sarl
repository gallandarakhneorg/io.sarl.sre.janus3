/*
 * $Id$
 *
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 *
 * Copyright (C) 2014-2018 the original authors or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License")
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package io.sarl.sre.boot.commands

import com.google.inject.AbstractModule
import com.google.inject.Module
import com.google.inject.Provider
import com.google.inject.Provides
import io.bootique.BQModule
import io.bootique.BQModuleProvider
import io.bootique.cli.Cli
import io.bootique.command.CommandOutcome
import io.bootique.meta.application.CommandMetadata
import io.sarl.sre.boot.configs.SreConfig
import javax.inject.Singleton

import static extension io.bootique.BQCoreModule.extend
import java.text.MessageFormat

/**
 * Command for running the SRE with a single agent at boot time.
 *
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 * @since 0.8
 */
class RunSingleAgentCommand extends AbstractRunCommand {

	/** Constructor.
	 *
	 * @param configuration the configuration of the tool.
	 */
	new (configuration : Provider<SreConfig>) {
		super(configuration,
			CommandMetadata
				.builder(typeof(RunSingleAgentCommand))
				.description(Messages::RunSingleAgentCommand_0))
	}

	override run(cli : Cli) : CommandOutcome {
		if (cli.standaloneArguments().isEmpty()) {
			return CommandOutcome.failed(255, Messages::RunSingleAgentCommand_1)
		}

		try {
			val bootstrap = getSREBootstrap
			if (bootstrap === null) {
				return CommandOutcome.failed(255, Messages::RunSingleAgentCommand_3)
			}
			var config = this.configuration.get
			var bootAgent = config.boot.getBootAgent(cli.class.classLoader)
			bootstrap.startAgent(bootAgent, config.boot.commandLineArgs)
		} catch (exception : Throwable) {
			return CommandOutcome.failed(255,
				MessageFormat::format(Messages::RunSingleAgentCommand_2, exception.localizedMessage), exception)
		}
		
		return CommandOutcome.succeeded
	}

}

/** Module for the command for running the SRE with a single agent.
 * 
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 * @since 0.9
 */
class RunSingleAgentCommandModule extends AbstractModule {

	override configure {
		binder.extend => [
			addCommand(typeof(RunSingleAgentCommand))
			defaultCommand = typeof(RunSingleAgentCommand)
		]
	}

	/** Provide the command for running the SRE.
	 * 
	 * @param configuration the configuration of the tool.
	 * @return the command.
	 */
	@Provides
	@Singleton
	def provideRunCommand(configuration : Provider<SreConfig>) : RunSingleAgentCommand {
		return new RunSingleAgentCommand(configuration)
	}

}

/** Provider of the module for running the SRE with a single agent.
 * 
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 * @since 0.9
 */
class RunSingleAgentCommandModuleProvider implements BQModuleProvider {

	override module : Module {
		return new RunSingleAgentCommandModule
	}

	override moduleBuilder : BQModule.Builder {
		return BQModule.builder(module).overrides(overrides).providerName(name).configs(configs).description(
			Messages::RunSingleAgentCommandModuleProvider_0);
	}

}
