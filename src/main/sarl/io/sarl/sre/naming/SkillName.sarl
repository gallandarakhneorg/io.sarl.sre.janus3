/* 
 * $Id$
 * 
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 * 
 * Copyright (C) 2014-2019 the original authors or authors.
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 * http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package io.sarl.sre.naming

import io.sarl.lang.core.Capacity
import io.sarl.lang.core.Skill
import io.sarl.lang.util.ClearableReference
import java.net.URI
import java.util.UUID
import org.eclipse.xtend.lib.annotations.Accessors

/** 
 * This class represents a skill name.
 * 
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 * @since 0.10
 */
class SkillName extends SarlName {

	transient var ^skill : ClearableReference<Skill>

	@Accessors
	val contextId : UUID

	@Accessors
	val spaceId : UUID

	@Accessors
	val agentId : UUID

	@Accessors
	val ^capacity : Class<? extends Capacity>

	/** Constructor.
	 *
	 * @param uri the uri of the context.
	 * @param contextId the identifier of the context.
	 * @param spaceId the identifier of the space.
	 * @param agentId the identifier of the agent.
	 * @param capacity the name of the capacity implemented by the skill.
	 */
	protected new (uri : URI, contextId : UUID, spaceId : UUID, agentId : UUID, ^capacity : Class<? extends Capacity>) {
		super(uri)
		this.contextId = contextId
		this.spaceId = spaceId
		this.agentId = agentId
		this.^capacity = ^capacity
	}

	override getAssociatedObject : Object {
		getAssociatedSkill.get
	}

	/** Replies the skill instance that is associated to this name.
	 * The associated skill may be {@code null}. In this case, it does
	 * not mean that the skill instance does not exist into the system.
	 * It means that the skill instance is not explicitly associated
	 * to the name.
	 * 
	 * @return the associated skill, or {@code null} if there is no
	 * skill with the given name or if the skill is not yet known.
	 */
	def getAssociatedSkill : ClearableReference<Skill> {
		if (this.^skill === null) {
			this.^skill = new ClearableReference(null)
		}
		return this.^skill
	}

	/** Change the skill instance that is associated to this name.
	 * The associated skill may be {@code null}. In this case, it does
	 * not mean that the skill instance does not exist into the system.
	 * It means that the skill instance is not explicitly associated
	 * to the name.
	 * 
	 * @param skill the associated skill, or {@code null} if there is no
	 * skill with the given name or if the skill is not yet known.
	 */
	def setAssociatedSkill(^skill : Skill) {
		assert ^skill === null || this.^capacity.isInstance(^skill)
		this.^skill = if(^skill === null) null else new ClearableReference(^skill)
	}

}
