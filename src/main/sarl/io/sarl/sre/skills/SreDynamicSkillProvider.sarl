/*
 * $Id$
 *
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 *
 * Copyright (C) 2014-2019 the original authors or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package io.sarl.sre.skills

import com.google.inject.Injector
import io.sarl.core.Behaviors
import io.sarl.core.DefaultContextInteractions
import io.sarl.core.ExternalContextAccess
import io.sarl.core.InnerContextAccess
import io.sarl.core.Lifecycle
import io.sarl.core.Logging
import io.sarl.core.Schedules
import io.sarl.core.Time
import io.sarl.lang.annotation.PrivateAPI
import io.sarl.lang.core.Agent
import io.sarl.lang.core.Capacity
import io.sarl.lang.core.DynamicSkillProvider
import io.sarl.lang.core.SREutils
import io.sarl.lang.core.Skill
import io.sarl.lang.util.ClearableReference
import io.sarl.sre.Kernel
import io.sarl.sre.capacities.InternalEventBusCapacity
import io.sarl.sre.capacities.InternalSchedules
import io.sarl.sre.capacities.MicroKernelCapacity
import io.sarl.sre.skills.bic.BehaviorsSkill
import io.sarl.sre.skills.bic.DefaultContextInteractionsSkill
import io.sarl.sre.skills.bic.ExternalContextAccessSkill
import io.sarl.sre.skills.bic.InnerContextAccessSkill
import io.sarl.sre.skills.bic.LifecycleSkill
import io.sarl.sre.skills.bic.LoggingSkill
import io.sarl.sre.skills.bic.SchedulesSkill
import io.sarl.sre.skills.bic.TimeSkill
import io.sarl.sre.skills.internal.InternalEventBusSkill
import io.sarl.sre.skills.internal.MicroKernelSkill
import javax.inject.Inject
import javax.inject.Singleton

/** 
 * Provider of the built-in capacities of the SRE platform.
 * 
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 * @since 0.6.0
 */
@SuppressWarnings("use_reserved_sarl_annotation")
@PrivateAPI(isCallerOnly = true)
@Singleton
class SreDynamicSkillProvider implements DynamicSkillProvider {

	val injector : Injector

	/** Constructor.
	 * 
	 * @param injector the injector.
	 */
	@Inject
	new (injector : Injector) {
		this.injector = injector
	}

	/** Inject fields into the given skill
	 *
	 * @return the {@code skill}.
	 */
	protected def inject(^skill : Skill) : Skill {
		this.injector.injectMembers(^skill)
		return ^skill
	}

	def installSkill(^agent : Agent, ^capacity : Class<? extends Capacity>) : ClearableReference<Skill> {
		var ^skill : Skill = null
		var capacities : Class<? extends Capacity>[]
		switch (^capacity) {
			case typeof(MicroKernelCapacity): {
				val kernel = this.injector.getInstance(typeof(Kernel))
				^skill = new MicroKernelSkill(^agent, kernel)
				capacities = #[^capacity]
			}
			case typeof(Logging): {
				^skill = new LoggingSkill(^agent)
				capacities = #[^capacity]
			}
			case typeof(Time): {
				^skill = new TimeSkill(^agent)
				capacities = #[^capacity]
			}
			case typeof(InternalEventBusCapacity): {
				^skill = new InternalEventBusSkill(^agent)
				capacities = #[^capacity]
			}
			case typeof(Lifecycle): {
				^skill = new LifecycleSkill(^agent)
				capacities = #[^capacity]
			}
			case typeof(Schedules),
			case typeof(InternalSchedules): {
				^skill = new SchedulesSkill(^agent)
				capacities = #[typeof(Schedules), typeof(InternalSchedules)]
			}
			case typeof(InnerContextAccess): {
				^skill = new InnerContextAccessSkill(^agent)
				capacities = #[^capacity]
			}
			case typeof(Behaviors): {
				^skill = new BehaviorsSkill(^agent)
				capacities = #[^capacity]
			}
			case typeof(ExternalContextAccess): {
				^skill = new ExternalContextAccessSkill(^agent)
				capacities = #[^capacity]
			}
			case typeof(DefaultContextInteractions): {
				^skill = new DefaultContextInteractionsSkill(^agent)
				capacities = #[^capacity]
			}
		}
		if (^skill !== null) {
			^skill.inject
			var selectedReference : ClearableReference<Skill>  = null
			assert capacities !== null
			for (cap : capacities) {
				var reference = ^agent.registerSkill(cap, ^skill)
				if (selectedReference === null && cap == ^capacity) {
					selectedReference = reference
				}
			}
			^skill.installSkill
			assert selectedReference !== null
			return selectedReference
		}
		return null
	}

	/** Register the given skill.
	 */
	protected def registerSkill(^agent : Agent, ^capacity : Class<? extends Capacity>,
		^skill : Skill) : ClearableReference<Skill> {
		SREutils::createSkillMapping(^agent, ^capacity, ^skill)
	}

	/** Install the given skill.
	 */
	protected def installSkill(^skill : Skill) {
		SREutils::doSkillInstallation(^skill)
	}

}
