/*
 * $Id$
 *
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 *
 * Copyright (C) 2014-2017 the original authors or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package io.janusproject.sre.services.time

import com.google.common.util.concurrent.Service
import io.janusproject.sre.services.AbstractDependentService
import java.util.concurrent.TimeUnit
import javax.inject.Singleton
import org.arakhne.afc.util.ListenerCollection

/**
 * Time service based on the JRE standard time.
 * 
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 * @since 2.0.6.0
 */
@Singleton
class JreTimeService extends AbstractDependentService implements TimeService {

	var listeners : ListenerCollection<TimeListener> = null

	final def getServiceType : Class<? extends Service> {
		typeof(TimeService)
	}

	/** Replies the OS current time.
	 *
	 * @return the OS current time in ms.
	 */
	def getOSCurrentTime : long {
		System::currentTimeMillis	
	}

	def getTimePrecision : TimeUnit {
		TimeUnit::MILLISECONDS
	}

	def getTime(timeUnit : TimeUnit) : double {
		val currentTime = this.OSCurrentTime
		val tu = timeUnit ?: TimeUnit::SECONDS
		if (tu == TimeUnit::MILLISECONDS) {
			return currentTime
		}
		return currentTime.convertFromTo(TimeUnit::MILLISECONDS, tu)
	}

	def getOSTimeFactor : double {
		1.0
	}

	def evolveTimeIfPossible(timeDelta : double) {
		fireTimeChanged
	}

	/** Notifies about time changes.
	 */
	protected def fireTimeChanged {
		var list : ListenerCollection<TimeListener>
		synchronized(this) {
			list = this.listeners
		}
		if (list !== null) {
			for (listener : list.getListeners(typeof(TimeListener))) {
				listener.timeChanged(this)
			}
		}
	}
	
	def addTimeListener(listener : TimeListener) {
		synchronized (this) {
			if (this.listeners === null) {
				this.listeners = new ListenerCollection
			}
			this.listeners.add(typeof(TimeListener), listener);
		}
	}
	
	def removeTimeListener(listener : TimeListener) {
		synchronized (this) {
			if (this.listeners !== null) {
				this.listeners.remove(typeof(TimeListener), listener);
				if (this.listeners.empty) {
					this.listeners = null
				}
			}
		}
	}
	
}
