/*
 * $Id$
 *
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 *
 * Copyright (C) 2014-2018 the original authors or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package io.janusproject.sre.services.lifecycle

import com.google.inject.AbstractModule
import com.google.inject.Injector
import io.sarl.lang.core.Agent
import io.sarl.lang.core.DynamicSkillProvider
import io.sarl.lang.core.SREutils
import io.sarl.sarlspecification.SarlSpecificationChecker
import java.lang.reflect.Constructor
import java.util.UUID
import javax.inject.Inject
import javax.inject.Provider
import io.sarl.lang.annotation.PrivateAPI

/** 
 * Implementation of a spawning service that is based on the other services of the Janus platform.
 * 
 * @author $Author: srodriguez$
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 */
class InjectionBasedLifecycleService extends AbstractLifecycleService {

	/** 
	 * Constructs the service with the given (injected) injector.
	 * 
	 * @param injector
	 * the injector that should be used by this service for creating the agents.
	 * @param sarlSpecificationChecker the tool for checking the validity of the SARL specification supported by
	 * the agents to launch.
	 */
	@Inject
	new (injector : Injector, sarlSpecificationChecker : SarlSpecificationChecker,
		skillProvider : DynamicSkillProvider) {
		super(sarlSpecificationChecker, new CreatorFactory(injector, skillProvider))
	}
	
	/** 
	 * An injection module that is able to inject the parent ID and agent ID when creating an agent.
	 * 
	 * @author $Author: sgalland$
	 * @version $FullVersion$
	 * @mavengroupid $GroupId$
	 * @mavenartifactid $ArtifactId$
	 */
	private static class CreatorFactory implements AgentCreatorProvider {

		val injector : Injector

		val skillProvider : DynamicSkillProvider 

		new (injector : Injector, skillProvider : DynamicSkillProvider) {
			this.injector = injector
			this.skillProvider = skillProvider
		}
	
		def getAgentCreator(agentClazz : Class<T>, parent : UUID,
			agentId : UUID, nbAgents : int) : Provider<T> with T extends Agent {
			val agentInjectionModule = new JustInTimeAgentInjectionModule<T>(agentClazz, parent, agentId, this.skillProvider)
			val agentInjector = this.injector.createChildInjector(agentInjectionModule)
			return [
				agentInjector.getInstance(agentClazz)
			]
		}
	
	}
	
	/** 
	 * An injection module that is able to inject the parent ID and agent ID when creating an agent.
	 * 
	 * @param <T> the type of the generated agent.
	 * @author $Author: sgalland$
	 * @version $FullVersion$
	 * @mavengroupid $GroupId$
	 * @mavenartifactid $ArtifactId$
	 */
	@SuppressWarnings("use_reserved_sarl_annotation")
	@PrivateAPI(isCallerOnly = true)
	private static class JustInTimeAgentInjectionModule<T extends Agent> extends AbstractModule implements Provider<T> {

		val agentType : Class<T>

		val constructor1 : Constructor<T>

		val constructor2 : Constructor<T> 

		val parentID : UUID

		val skillProvider : DynamicSkillProvider

		var agentID : UUID

		package new (agentType : Class<T>, parentID : UUID, agentID : UUID, skillProvider : DynamicSkillProvider) {
			assert agentType !== null
			assert parentID !== null
			this.agentType = agentType
			this.parentID = parentID
			this.agentID = agentID
			this.skillProvider = skillProvider
			var cons : Constructor<T> = null
			var e1 : Exception = null
			try {
				cons = this.agentType.getConstructor(typeof(UUID), typeof(UUID))
			} catch (exception : Exception) {
				cons = null
				e1 = exception
			}
			this.constructor1 = cons
			var e2 : Exception = null
			try {
				cons = this.agentType.getConstructor(typeof(UUID), typeof(UUID), typeof(DynamicSkillProvider))
			} catch (exception : Exception) {
				cons = null
				e2 = exception
}
			this.constructor2 = cons
			if (this.constructor1 === null && this.constructor2 === null) {
				throw new CannotSpawnException(this.agentType, e1 ?: e2)
			}
		}

		def configure {
			bind(this.agentType).toProvider(this)
		}

		synchronized def get : T {
			var aid = this.agentID ?: UUID::randomUUID
			this.agentID = null
			assert this.constructor1 !== null || this.constructor2 !== null
			try {
				if (this.constructor2 !== null) {
					return this.constructor2.newInstance(this.parentID, aid, this.skillProvider)
				}
				var ^agent = this.constructor1.newInstance(this.parentID, aid)
				SREutils::setDynamicSkillProvider(^agent, this.skillProvider)
				return ^agent
			} catch (exception : Exception) {
				throw new CannotSpawnException(this.agentType, exception)
			}
		}

	}

}
